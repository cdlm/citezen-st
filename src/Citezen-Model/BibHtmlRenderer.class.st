"
Render bibliographic entries to HTML using Seaside's canvas.

NB. Both BibHtmlRenderer and the canvas know about the stream, so using #setStream: after #html: will break stuff.
"
Class {
	#name : #BibHtmlRenderer,
	#superclass : #BibStreamRenderer,
	#instVars : [
		'html',
		'context',
		'document',
		'docRoot'
	],
	#category : #'Citezen-Rendering'
}

{ #category : #'instance creation' }
BibHtmlRenderer class >> html: anHtmlCanvas [
	^ (self new)
		html: anHtmlCanvas;
		yourself
]

{ #category : #accessing }
BibHtmlRenderer >> contents [
	^ self html document stream contents
]

{ #category : #accessing }
BibHtmlRenderer >> html [
	^ html ifNil: [
		html := WAStaticHtmlCanvas document:
			(WAHtmlStreamDocument new stream: stream)]
]

{ #category : #accessing }
BibHtmlRenderer >> html: anHtmlCanvas [
	html := anHtmlCanvas.
	self setStream: html document stream
]

{ #category : #'visitor-double dispatch' }
BibHtmlRenderer >> visitFieldRef: aFieldRef [
	self html text: (currentEntry at: aFieldRef fieldName) value
]

{ #category : #'visitor-double dispatch' }
BibHtmlRenderer >> visitLiteralPhrase: aPhrase [
	self html text: aPhrase text
]

{ #category : #'visitor-double dispatch' }
BibHtmlRenderer >> visitNamedPhrase: aPhrase [ 
	(aPhrase isBlock ifTrue: [self html div] ifFalse: [self html span])
		class: aPhrase name;
		with: [super visitNamedPhrase: aPhrase]
]
