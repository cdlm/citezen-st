Class {
	#name : #CZStefScript,
	#superclass : #Object,
	#instVars : [
		'document'
	],
	#category : #'Citezen-Script'
}

{ #category : #script }
CZStefScript >> articleDisplayPattern [

	^ CZDisplayPattern new 
			defaultSeparator: ', ';
			field: #author;
			field: #title;
			field: #journal;
			field: #volume;
			field: #pages;
			field: #publisher; 
			field: #year;
		"	separator: '---'; "
			field: #impactfactor;
			field: #url;
			field: #doi;
			endsWith: '.'
]

{ #category : #script }
CZStefScript >> assemble [
	"self new assemble"
	
	self inout.
	self document 
		orderedList ;
		doNotRestartOrderedListAtEachScript;
		addElement: (CZTitleElement new 
							title: 'Refereed Articles in International Journals'; htmlLevel: 4);
		addElement: self journalsScript;
		addElement: (CZTitleElement new 
							title: 'Refereed Articles in Top Conferences'; htmlLevel: 4);
		addElement: self topConferencesScript.
	self document exportAsHtml. 
	self document generate.
	^ self document
]

{ #category : #script }
CZStefScript >> conferenceDisplayPattern [

	^ CZDisplayPattern new 
			defaultSeparator: ', ';
			field: #author;
			field: #title;
			field: #booktitle;
			field: #volume;
			field: #pages;
			field: #publisher; 
			field: #year;
		"	separator: '---'; "
			field: #impactFactor;
			field: #url;
			field: #doi;
			endsWith: '.'
]

{ #category : #script }
CZStefScript >> document [

	^ document ifNil: [document := CZDocument new]
]

{ #category : #script }
CZStefScript >> inout [

	self document
		inputFile: 'scg.bib';
		outputFile: 'stef.html'.
]

{ #category : #script }
CZStefScript >> journalsScript [
	^ CZScript new
		displayPattern: self articleDisplayPattern;
		reversedSortByField: #year;
		query: (self onlyStefAuthor 
					AND: (CZBibFieldQuery 
								named: #annote 
								checks: [ :value | value = 'internationaljournal' ]))
]

{ #category : #script }
CZStefScript >> metadata [

	self document addElement: (CZCommentElement new comment: 'Generated by Citezen --', Date today printString).
]

{ #category : #script }
CZStefScript >> onlyStefAuthor [
	^ CZBibFieldQuery named: #author checks: [ :value | '*Ducasse*' match: value ]
]

{ #category : #script }
CZStefScript >> topConferencesScript [
	^ CZScript new
		displayPattern: self articleDisplayPattern;
		reversedSortByField: #year;
		query: (self onlyStefAuthor 
					AND: (CZBibFieldQuery 
								named: #annote 
								checks: [ :value | '*topconference*' match: value ]))
]
